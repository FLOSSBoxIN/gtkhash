_realname=gtkhash
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=git
pkgrel=1
arch=( any )
pkgdesc="Desktop utility for computing message digests or checksums (mingw-w64)"
depends=(
	${MINGW_PACKAGE_PREFIX}-gtk3
	${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme
	${MINGW_PACKAGE_PREFIX}-libgcrypt
)
makedepends=(
	${MINGW_PACKAGE_PREFIX}-libtool
	${MINGW_PACKAGE_PREFIX}-pkg-config
	git
)
options=( strip )
license=( GPL )
url="https://github.com/tristanheaven/gtkhash"
install=${_realname}-${CARCH}.install
source=( ${_realname}.tar.gz )
sha256sums=( SKIP )

pkgver() {
	git describe --always | sed -e 's/v//' -e 's/-/+/g'
}

_getdir() {
	echo "${srcdir}"/${_realname}-*
}

build() {
	cd "$(_getdir)"
	meson _build \
		--prefix="$MINGW_PREFIX" \
		-Db_lto=true \
		-Dappstream=false \
		-Dblake2=false \
		-Dlinux-crypto=false \
		-Dlibcrypto=false \
		-Dmbedtls=false \
		-Dnettle=false \
		-Dzlib=false
	ninja -C _build
}

check() {
	cd "$(_getdir)"
	ninja -C _build test
}

package() {
	cd "$(_getdir)"
	DESTDIR="${pkgdir}" ninja -C _build install
}
